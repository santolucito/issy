---------------------------------------------------------------------------------------------------
{-# LANGUAGE LambdaCase #-}

---------------------------------------------------------------------------------------------------
module Issy.Logic.SMT
  ( sat
  , satModel
  , trySat
  , trySatModel
  , unsat
  , valid
  , -- Simplification
    simplify
  , simplifyUF
  , trySimplify
  , trySimplifyUF
  , -- Max SMT
    optPareto
  , tryOptPareto
  ) where

---------------------------------------------------------------------------------------------------
import Data.Map ((!?))
import qualified Data.Set as Set
import System.Exit (die)

import Issy.Config (Config, Solver(..), solver, z3cmd, cvc5cmd)
import Issy.Logic.FOL (Model, Sort, Symbol, Term)
import qualified Issy.Logic.FOL as FOL
import qualified Issy.Parsers.SMTLib as SMTLib
import qualified Issy.Parsers.SMTLibLexer as SMTLib
import qualified Issy.Printers.SMTLib as SMTLib
import Issy.Utils.Extra (noTimeout, runTO)
import Issy.Utils.Logging

---------------------------------------------------------------------------------------------------
-- SMT Solving
---------------------------------------------------------------------------------------------------
sat :: Config -> Term -> IO Bool
sat conf = noTimeout . trySat conf Nothing

unsat :: Config -> Term -> IO Bool
unsat conf f = not <$> sat conf f

valid :: Config -> Term -> IO Bool
valid conf f = not <$> sat conf (FOL.neg f)

satModel :: Config -> Term -> IO (Maybe Model)
satModel conf = noTimeout . trySatModel conf Nothing

trySat :: Config -> Maybe Int -> Term -> IO (Maybe Bool)
trySat conf to f
  | f == FOL.true = pure $ Just True
  | f == FOL.false = pure $ Just False
  | otherwise = do
    let query = SMTLib.toQuery f ++ satCommand (solver conf) f
    callSolver conf to query $ \case
      'u':'n':'s':'a':'t':_ -> Just False
      's':'a':'t':_ -> Just True
      _ -> Nothing

trySatModel :: Config -> Maybe Int -> Term -> IO (Maybe (Maybe Model))
trySatModel conf to f = do
  let query = SMTLib.toQuery f ++ satCommand (solver conf) f ++ "(get-model)"
  callSolver conf to query $ \case
    'u':'n':'s':'a':'t':_ -> Just Nothing
    's':'a':'t':xr -> Just $ Just $ SMTLib.extractModel (FOL.decls f) xr
    _ -> Nothing

satCommand :: Solver -> Term -> String
satCommand slv f =
  case slv of
    Z3
      | FOL.SInt `elem` FOL.sorts f && FOL.SReal `elem` FOL.sorts f && not (FOL.quantifierFree f) ->
        "(check-sat-using (and-then qe default))"
      | otherwise -> "(check-sat)"
    CVC5 -> "(check-sat)"

---------------------------------------------------------------------------------------------------
-- Simplification
---------------------------------------------------------------------------------------------------
z3SimplifyQE :: [String]
z3SimplifyQE =
  ["simplify", "qe-light", "propagate-ineqs", "unit-subsume-simplify", "qe2", "simplify"]

z3Simplify :: [String]
z3Simplify =
  [ "simplify"
  , "blast-term-ite"
  , "nnf"
  , "ctx-solver-simplify"
  , "propagate-ineqs"
  , "unit-subsume-simplify"
  , "solver-subsumption"
  , "simplify"
  ]

z3SimplifyUF :: [String]
z3SimplifyUF = ["simplify", "blast-term-ite", "nnf", "propagate-ineqs", "qe", "simplify"]

simplify :: Config -> Term -> IO Term
simplify conf = noTimeout . trySimplify conf Nothing

trySimplify :: Config -> Maybe Int -> Term -> IO (Maybe Term)
trySimplify conf to term = do
  term <- simplifyTacs conf to z3SimplifyQE term
  case term of
    Nothing -> pure Nothing
    Just term -> do
      term <- simplifyTacs conf to z3Simplify $ FOL.toNNF $ FOL.neg term
      case term of
        Nothing -> pure Nothing
        Just term -> simplifyTacs conf to z3Simplify $ FOL.toNNF $ FOL.neg term

simplifyTacs :: Config -> Maybe Int -> [String] -> Term -> IO (Maybe Term)
simplifyTacs conf to tactics f
  | f == FOL.true || f == FOL.false = pure (Just f)
  | solver conf == CVC5 = pure (Just f)  -- CVC5 doesn't support Z3's tactic system
  | FOL.ufFree f = do
    let query = SMTLib.toQuery f ++ "(apply " ++ z3TacticList tactics ++ ")"
    callSolver conf to query $ \res ->
      case readTransformZ3 (FOL.bindings f !?) (SMTLib.tokenize res) of
        Right res -> Just res
        _ -> Nothing
  | otherwise = pure $ Just f

simplifyUF :: Config -> Term -> IO Term
simplifyUF conf = noTimeout . trySimplifyUF conf Nothing

trySimplifyUF :: Config -> Maybe Int -> Term -> IO (Maybe Term)
trySimplifyUF conf to f
  | f == FOL.true || f == FOL.false = pure (Just f)
  | solver conf == CVC5 = pure (Just f)  -- CVC5 doesn't support Z3's tactic system
  | otherwise = do
    let query = SMTLib.toQuery f ++ "(apply " ++ z3TacticList z3SimplifyUF ++ ")"
    callSolver conf to query $ \res ->
      case readTransformZ3 (FOL.bindings f !?) (SMTLib.tokenize res) of
        Right res -> Just res
        _ -> Nothing

z3TacticList :: [String] -> String
z3TacticList =
  \case
    [] -> error "assertion: non-empty tactic list not allowed"
    [t] -> t
    t:tr -> "(and-then " ++ t ++ " " ++ z3TacticList tr ++ ")"

readTransformZ3 :: (Symbol -> Maybe Sort) -> [SMTLib.Token] -> Either String Term
readTransformZ3 ty =
  \case
    SMTLib.TLPar:SMTLib.TId "goals":SMTLib.TLPar:SMTLib.TId "goal":tr -> FOL.andf <$> readGoals tr
    ts -> Left $ "Invalid pattern for goals: " ++ show ts
  where
    readGoals =
      \case
        [] -> Left "assertion: found [] before ')' while reading goals"
        SMTLib.TId (':':_):_:tr -> readGoals tr
        [SMTLib.TRPar, SMTLib.TRPar] -> Right []
        ts ->
          case SMTLib.parseTerm ty ts of
            Left err -> Left err
            Right (f, tr) -> (f :) <$> readGoals tr

---------------------------------------------------------------------------------------------------
-- Optimal Solving
---------------------------------------------------------------------------------------------------
optPareto :: Config -> Term -> [Term] -> IO (Maybe Model)
optPareto conf f = noTimeout . tryOptPareto conf Nothing f

tryOptPareto :: Config -> Maybe Int -> Term -> [Term] -> IO (Maybe (Maybe Model))
tryOptPareto conf to f maxTerms = do
  f <- simplify conf f
  if not (FOL.quantifierFree f)
    then trySatModel conf to f
    else let maxQueries =
               concatMap (\t -> "(maximize " ++ SMTLib.toString t ++ ")")
                 $ filter ((`Set.isSubsetOf` FOL.frees f) . FOL.frees) maxTerms
             query =
               "(set-option :opt.priority pareto)"
                 ++ SMTLib.toQuery f
                 ++ maxQueries
                 ++ "(check-sat)(get-model)"
          in callSolver conf to query $ \case
               'u':'n':'s':'a':'t':_ -> Just Nothing
               's':'a':'t':xr -> Just $ Just $ SMTLib.extractModel (FOL.frees f) xr
               _ -> Nothing

---------------------------------------------------------------------------------------------------
-- Helper and common routines
---------------------------------------------------------------------------------------------------
callSolver :: Config -> Maybe Int -> String -> (String -> Maybe a) -> IO (Maybe a)
callSolver conf to query parse = do
  let (cmd, args, solverName) = case solver conf of
        Z3   -> (z3cmd conf, ["-smt2", "-in"], "z3")
        CVC5 -> (cvc5cmd conf, ["--lang=smt2", "--produce-models"], "cvc5")
  lgv conf [solverName ++ " query:", query]
  res <- runTO to cmd args query
  case res of
    Nothing -> pure Nothing
    Just res ->
      case parse res of
        Just res -> pure (Just res)
        _ -> die $ solverName ++ " returned unexpected: \"" ++ res ++ "\" on \"" ++ query ++ "\""
---------------------------------------------------------------------------------------------------
